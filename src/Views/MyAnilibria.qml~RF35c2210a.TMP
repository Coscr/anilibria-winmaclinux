import QtQuick 2.12
import QtQuick.Controls 2.12
import QtQuick.Layouts 1.3
import QtGraphicalEffects 1.0
import "../Controls"

Page {
    id: myAnilibriaPage
    anchors.fill: parent

    signal navigateFrom()
    signal navigateTo()

    background: Rectangle {
        color: applicationThemeViewModel.pageBackground
    }

    Loader {
        anchors.fill: parent
        sourceComponent: myAnilibriaPage.visible ? myAnilibriaContent : null
    }

    Component {
        id: myAnilibriaContent

        Item {
            anchors.fill: parent

            RowLayout {
                id: panelContainer
                anchors.fill: parent
                spacing: 0
                Rectangle {
                    color: applicationThemeViewModel.pageVerticalPanel
                    width: 40
                    Layout.fillHeight: true
                    Column {
                        IconButton {
                            height: 45
                            width: 40
                            overlayVisible: false
                            iconPath: assetsLocation.iconsPath + "coloreddrawer.svg"
                            iconWidth: 28
                            iconHeight: 28
                            onButtonPressed: {
                                drawer.open();
                            }
                        }
                    }
                }

                ColumnLayout {
                    Layout.fillHeight: true
                    Layout.fillWidth: true
                    spacing: 2

                    Rectangle {
                        Layout.fillWidth: true
                        Layout.preferredHeight: 45
                        height: 45
                        color: applicationThemeViewModel.pageUpperPanel

                        PlainText {
                            anchors.verticalCenter: parent.verticalCenter
                            anchors.horizontalCenter: parent.horizontalCenter
                            visible: releasesViewModel.synchronizationEnabled
                            fontPointSize: 12
                            text: "Выполняется синхронизация..."
                        }

                        RoundedActionButton {
                            id: makeStartPageButton
                            buttonEnabled: userConfigurationViewModel.startPage !== "myanilibria"
                            anchors.verticalCenter: parent.verticalCenter
                            anchors.left: parent.left
                            width: 160
                            textSize: 10
                            text: "Сделать стартовой"
                            onClicked: {
                                userConfigurationViewModel.startPage = "myanilibria";
                            }
                        }

                        TextField {
                            id: filterTextField
                            anchors.left: makeStartPageButton.right
                            anchors.leftMargin: 10
                            width: 250
                            selectByMouse: true
                            placeholderText: "Введите название релиза"
                            onTextChanged: {
                                myAnilibriaSearchListModel.filter = text;
                            }

                            FilterPanelIconButton {
                                visible: filterTextField.text.length
                                anchors.right: parent.right
                                anchors.top: parent.top
                                anchors.topMargin: 6
                                width: 20
                                height: 20
                                iconPath: assetsLocation.iconsPath + "closewindow.svg"
                                iconColor: applicationThemeViewModel.plainTextColor
                                iconWidth: 16
                                iconHeight: 16
                                tooltipMessage: "Очистить фильтр"
                                onButtonPressed: {
                                    filterTextField.text = "";
                                }
                            }
                        }

                        IconButton {
                            id: openAllButton
                            anchors.right: closeAllButton.left
                            height: 45
                            width: 40
                            overlayVisible: false
                            iconPath: assetsLocation.iconsPath + "plus.svg"
                            iconWidth: 28
                            iconHeight: 28
                            tooltipMessage: "Открыть все секции"
                            onButtonPressed: {
                                myAnilibriaViewModel.myList.setNotVisibleAllMarks(false);
                            }
                        }

                        IconButton {
                            id: closeAllButton
                            anchors.right: sectionVisibilities.left
                            height: 45
                            width: 40
                            overlayVisible: false
                            iconPath: assetsLocation.iconsPath + "minus.svg"
                            iconWidth: 28
                            iconHeight: 28
                            tooltipMessage: "Закрыть все секции"
                            onButtonPressed: {
                                myAnilibriaViewModel.myList.setNotVisibleAllMarks(true);
                            }
                        }

                        IconButton {
                            id: sectionVisibilities
                            anchors.right: parent.right
                            height: 45
                            width: 40
                            overlayVisible: false
                            iconPath: assetsLocation.iconsPath + "videos.svg"
                            iconWidth: 28
                            iconHeight: 28
                            tooltipMessage: "Изменить видимость секций станицы"
                            onButtonPressed: {
                                sectionVisibilitiesPopup.open();
                            }

                            DefaultPopup {
                                id: sectionVisibilitiesPopup
                                x: -420
                                width: 400
                                height: 200
                                modal: true
                                focus: true
                                closePolicy: Popup.CloseOnEscape | Popup.CloseOnPressOutsideParent

                                ListView {
                                    id: allSectionList
                                    anchors.fill: parent
                                    clip: true
                                    model: myAnilibriaViewModel.allSections
                                    delegate: Item {
                                        width: allSectionList.width
                                        height: 40

                                        CheckBox {
                                            checked: sectionSelected
                                            text: sectionTitle
                                            onCheckedChanged: {
                                                if (checked) {
                                                    myAnilibriaViewModel.selectSection(sectionId)
                                                } else {
                                                    myAnilibriaViewModel.deselectSection(sectionId)
                                                }
                                            }
                                        }
                                    }
                                    ScrollBar.vertical: ScrollBar {
                                        active: true
                                    }
                                }
                            }
                        }
                    }

                    ListView {
                        id: scrollview
                        Layout.fillHeight: true
                        Layout.fillWidth: true
                        clip: true
                        model: myAnilibriaViewModel.myList
                        delegate: Component {
                            Loader {
                                source: "./MyAnilibria/" + componentId + ".qml"
                            }
                        }
                        ScrollBar.vertical: ScrollBar {
                            active: true
                        }
                    }
                }
            }

            Item {
                visible: myAnilibriaViewModel.hoveredDescription.length
                enabled: false
                anchors.left: parent.left
                anchors.leftMargin: 42
                anchors.bottom: parent.bottom
                anchors.rightMargin: 2
                height: 105
                width: parent.width / 2

                Rectangle {
                    anchors.fill: parent
                    color: applicationThemeViewModel.pageBackground
                    opacity: 0.8
                }

                PlainText {
                    anchors.fill: parent
                    fontPointSize: 11
                    text: myAnilibriaViewModel.hoveredDescription
                    wrapMode: Text.WordWrap
                    textFormat: Text.RichText
                    maximumLineCount: 5
                    verticalAlignment: Text.AlignVCenter
                }
            }

            Item {
                id: findedReleasesContainer
                x: filterTextField.x
                y: filterTextField.y + filterTextField.height
                width: 400
                height: findedReleases.height
                visible: myAnilibriaSearchListModel.hasItems
                Rectangle {
                    anchors.fill: parent
                    color: applicationThemeViewModel.panelBackground
                    radius: 8
                }

                ListView {
                    id: findedReleases
                    anchors.top: parent.top
                    anchors.topMargin: 4
                    anchors.left: parent.left
                    anchors.leftMargin: 10
                    anchors.right: parent.right
                    anchors.rightMargin: 10
                    width: parent.width
                    height: contentHeight
                    model: myAnilibriaSearchListModel
                    delegate: Item {
                        width: findedReleases.width
                        height: 60

                        Rectangle {
                            id: hoveredRectangle
                            visible: false
                            anchors.top: parent.top
                            anchors.left: parent.left
                            anchors.leftMargin: -4
                            anchors.right: parent.right
                            anchors.rightMargin: -4
                            anchors.bottom: parent.bottom
                            anchors.bottomMargin: 10
                            color: applicationThemeViewModel.panelBackgroundShadow
                        }

                        MouseArea {
                            anchors.fill: parent
                            hoverEnabled: true
                            onEntered: {
                                hoveredRectangle.visible = true;
                            }
                            onExited: {
                                hoveredRectangle.visible = false;
                            }
                        }

                        Column {
                            anchors.fill: parent

                            Item {
                                width: parent.width
                                height: 30

                                AccentText {
                                    anchors.verticalCenter: parent.verticalCenter
                                    verticalAlignment: Text.AlignVCenter
                                    width: 300
                                    height: 30
                                    fontPointSize: 10
                                    text: title
                                    maximumLineCount: 1
                                    elide: Text.ElideRight
                                    wrapMode: Text.NoWrap
                                }

                                PlainText {
                                    anchors.right: parent.right
                                    anchors.verticalCenter: parent.verticalCenter
                                    fontPointSize: 9
                                    text: year
                                }
                            }

                            Item {
                                width: parent.width
                                height: 30

                                PlainText {
                                    anchors.top: parent.top
                                    width: 300
                                    height: 30
                                    fontPointSize: 9
                                    text: originalName
                                    maximumLineCount: 1
                                    elide: Text.ElideRight
                                    wrapMode: Text.NoWrap
                                }

                                PlainText {
                                    anchors.top: parent.top
                                    anchors.verticalCenter: parent.verticalCenter
                                    anchors.right: parent.right
                                    fontPointSize: 9
                                    text: season
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

